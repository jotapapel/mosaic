import graphics from "./graphics.tle"

@export @default var font = [
	"SIZE": 8,
	"STYLE_PLAIN": [
		"ADDRESS": &20,
		"WIDTH": 5,
		"ADJUST": [
			"1": "#%*%?$&%^mw",
			"-1": "\"%%%+/<>\\{}",
			"-2": "%(%),;%[%]`1jl",
			"-3": "!\'%.:|i"
		]
	],
	"STYLE_BOLD": [
		"ADDRESS": &80,
		"WIDTH": 6,
		"ADJUST": [
			"3": "mw",
			"2": "#",
			"1": "%*%?MW^~",
			"-1": "%%%+/<>\\{}",
			"-2": "%(%)1,;%[%]`jl",
			"-3": "!\'%.:|i"
		]
	]
]

function font.print (text, x, y, color, style)
	var width = 0
	style = style or (x is "table" and x) or font.STYLE_PLAIN
	graphics.pal(graphics.BRIGHT_WHITE, color or graphics.BLACK)
	for index = 1 to string.len(text) do
		var char = string.sub(text, index, index), charw = style.WIDTH
		for [adjw, adjp] in pairs(style.ADJUST) do
			if string.match(char, "[" + adjp + "]") then
				charw += adjw
			end
		end
		if string.match(char, "[%d%u]") then
			charw += 1
		elseif string.match(char, "%s") and index > 1 then
			width -= 1
		end
		if x and y and string.match(char, "%C") then
			spr(style.ADDRESS + string.byte(char) - 32, x + width, y, 0)
		end
		width += charw
	end
	graphics.pal()
	return width - 1
end